
**
** Oxymonacanthus + vendor script
**
** Sprites by Jesseth, coding by Pilla.
**
**
** Feel free to use this code however you want.
**
**

**
** Caos2pray chunk
**
** Compiles the agent.
**
* CAOS2PRAY CHUNK
*# Pray-File "gp_oxymonacanthus_JESS.agents"
*# DS-Name "GP - Oxymonacanthus"
*# attach gp_nudioxy_JESS.c16 
*# depend blnk.c16
*# desc = "Cute oxymonacanthus to liven up your aquatic areas.\nSprites by Jesseth, coding by Pilla"
*# Agent Animation File = "gp_nudioxy_JESS.c16"
*# Agent Sprite First Image = 53
*# Agent Animation Gallery = "gp_nudioxy_JESS"
*
*

*
** Injection script
**
*new: comp 2 23 21851 "gp_nudioxy_JESS" 1 0 rand 800 900
*mesg wrt+ targ 1000 game "CreatorX" game "CreatorY" 0


**
**
** SCRIPTS
**
**
**



*
** Egg creator script
scrp 2 18 21853 1000
	attr 192
	elas 0
	fric 100
	setv ov55 2
	doif tmvt _p1_ _p2_ ne 1
		kill targ
		stop
	endi
	tick rand 100 600
	mvto _p1_ _p2_
endm

*
** Fish creator script
scrp 2 16 21853 1000
	frat 4
	attr 195
	elas 50
	accg 0
	aero 60
* set ov10 (direction) to -1 or 1.
	loop
		setv ov10 rand -1 1
	untl ov10 ne 0
	perm 60
	tick rand 6 10
	setv ov02 rand 200 600
	setv ov61 50
	mvto _p1_ _p2_
endm

*
** Fish timer script
scrp 2 16 21853 9
	tick rand 8 14
** Check if out of water
	doif rtyp room targ ne 8 and rtyp room targ ne 9
** increase out of water timer ov99
		addv ov99 1
** Fall down
		accg 4
		aero 10
		setv vely 3

		wait 10
** If still not in water
** splash on side
		doif rtyp room targ ne 8 and rtyp room targ ne 9
**sndc "fish"
			anim [0 2 255]
			accg 4
			aero 10
			loop
				wait 1
			untl fall eq 0
** Splash up/down/left/right
			velo rand -5 5 rand -10 -5
** if on land for 20 turns, u ded.
			doif ov99 ge 20
				stpc
				anim [4]
				wait 30
				kill ownr
			endi
			stop
		endi
	else
** if in water, reset out of water timer ov99 and float
		setv ov99 0
		accg 0
		aero 0
	endi


** Do this randomly 1 in 10 times.
	doif rand 0 19 eq 0 and base ne 0
		rnge 1800
		doif carr eq null
			inst
			setv va20 0
** check for fish
			esee 2 16 21853
				addv va20 1
			next
** check for eggs
			esee 2 18 21853
				addv va20 1
			next
** if total less than/equal to 15
			doif va20 le 15

				rnge 600
				inst
				setv va20 0
** check for fish
				esee 2 16 21853
					addv va20 1
				next
** check for eggs
				esee 2 18 21853
					addv va20 1
				next
				doif va20 le 7


** lay an egg
					setv va00 posl
					setv va01 post
					new: simp 2 18 21853 "gp_nudioxy_JESS" 1 37 10
					mesg wrt+ targ 1000 va00 va01 0
				endi
				slow
				targ ownr
			endi
		endi
	endi

** check for obstacle left and moving left
	doif obst left lt 25 and ov10 lt 0
		negv ov10
	endi
** check for obstacle right and moving right
	doif obst rght lt 25 and ov10 gt 0
		negv ov10
	endi

** if moving left, animate lefty
	doif ov10 lt 0
		setv velx rand -1 -3
		anim [0 1 2 3 255]
	else
** if moving right, animate righty
		setv velx rand 1 3
		anim [5 6 7 8 255]
	endi

** set vertical velocity randomly

	setv va50 posx
	setv va51 posy
	subv va51 15
	setv vely rand -1 1
	doif grap va50 va51 ne -1
		doif rtyp grap va50 va51 ne 8 and rtyp grap va50 va51 ne 9
			setv vely rand 0 1
		endi
	endi

	subv ov02 1
** If tiny, grow
	doif ov02 le 300 and base eq 0
		base 10
	endi
** If life force up, ded
	doif ov02 le 0
		velo 0 0
		frat 40
		inst
		doif ov10 lt 0
			anim [4 4 4 4]
		else
			anim [9 9 9 9]
		endi
		mvsf va50 va51
		over
		inst
** add nutrients to room
		doif room targ <> -1
			altr room targ 4 0.1
			altr room targ 3 0.1
		endi
		kill ownr
	endi

endm

** Egg timer script
scrp 2 18 21853 9
** grow
	doif ov55 gt 0
		subv ov55 1
	else
		setv va00 posl
		setv va01 post
		subv va01 8
		subv va00 8

** Create fish
		inst
		new: simp 2 16 21853 "gp_nudioxy_JESS" 20 38 1000
		frat 4
		attr 195
		elas 50
		accg 0
		setv ov10 rand 0 1
		doif ov10 eq 0
			setv ov10 -1
		endi
		perm 60
		setv va99 0
		setv va98 0
** Try to find a safe spot to move fish
		loop
			addv va99 1
			doif tmvt va00 va01 ne 1
				subv va01 1
			else
				mvto va00 va01
				setv va98 1
			endi
		untl va99 ge 40

** If this fails, KILL
		doif va98 eq 0
			kill targ
		else
** If it succeeds, move the new fish to its new biome. <3
			tick rand 6 7
			setv ov02 400

			setv ov61 50
			mvto va00 va01
		endi
		kill ownr


	endi
endm

** On egg pickup
scrp 2 16 12853 4
	setv ov99 1
endm

** On egg drop
scrp 2 16 21853 5
	setv ov99 0
endm

** Exception
scrp 2 16 21853 255
	kill ownr
endm

scrp 2 18 21853 255
	kill ownr
endm





**
** Removal script
**

rscr
enum 2 16 21853
	kill targ
next
enum 2 18 21853
	kill targ
next
scrx 2 16 21853 9
scrx 2 16 21853 6