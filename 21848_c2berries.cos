**
**
**
**
** GP grendel hideout berries
**
** Create berry: 
*	ov04 base
*	ov05 posx
*	ov06 width
*	ov07 posy
*	ov08 height
**
**

** Scripts
**

*
** Berry creator timer script
*
scrp 1 1 21848 9

* Set pose variables
	setv va00 ov04
	mulv va00 3
* Set dimension variables posx width posy length
	setv va01 ov05
	addv va01 rand 0 ov06
	setv va02 ov07
	addv va02 rand 0 ov08
* Create patch berry
	new: simp 2 8 21848 "gp_c2berries" 9 0 rand 50 1000
	attr 80
	perm rand 20 100
	emit 6 .3
	elas 25
	fric 90
	setv ov01 0
	tick 1
	base va00
	pose 0
	mvsf va01 va02
	targ ownr
	tick rand 100 350
endm


*
** Berry timer script
*

scrp 2 8 21848 9
	wait rand 200 800
	addv ov01 1
	pose ov01
	setv ov00 1
	wait rand 100 300
	pose 2
	attr 199
	accg 3
	aero 2
	bhvr 48
	wait rand 100 1000
		setv va00 posl
		setv va01 post
		setv va02 perm
		new: simp 2 10 21848 "gp_c2berries_rotten" 1 0 rand 500 1000
		mvto va00 va01
		perm va02
		tick rand 800 1500
		kill ownr
endm

*
** Berry eat script
*

scrp 2 8 21848 12
	sndc "chwp"
	stim writ from 78 .5
	chem 5 1
	chem 10 .2
	chem 12 .2
	chem 5 .2
	targ ownr
	kill targ
endm

**
** Removal script
**
rscr

* Remove berry creator
enum 1 1 21848
	kill targ
next

* Remove berries
enum 2 8 21848
	kill targ
next