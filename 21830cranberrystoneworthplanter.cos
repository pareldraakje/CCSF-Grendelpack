**
** Cranberry Stoneworth planter script
**
** Sprites by Jesseth, coding by Pilla.
**
**
** Feel free to use this code however you want.
**
**

**
** Caos2pray chunk
**
** Compiles the agent.
**
* CAOS2PRAY CHUNK
*# Pray-File "gp_cranstone_planter_JESS.agents"
*# DS-Name "Cranberry stoneworth planter(Jesseth)"
*# attach gp_cranstone_planter_JESS.c16 
*# desc = "A cranstone planter"
*# Agent Animation File = "gp_cranstone_planter_JESS.c16"
*# Agent Sprite First Image = 0
*# Agent Animation Gallery = "gp_cranstone_planter_JESS"
*
*

*
** Injection script
**

**
** The planter
**

*new: simp 1 1 21830 "gp_cranstone_planter_JESS" 1 0 rand 700 900
*mesg wrt+ targ 1000 Game "CreatorX" Game "CreatorY" 0

**
** SCRIPTS
**

**
** PLANTER CREATOR SCRIPT
**

scrp 1 1 21830 1000
	inst
	new: comp 2 23 21830 "gp_cranstone_planter_JESS" 1 0 rand 700 900
** Attributes: mouseable 2 + activateable 4 + invisible 16 + collide 64 + physics 128 = 214
	attr 214
	perm 100
	accg 0
	elas 0
	fric 100
	aero 100
	perm 100
	bhvr 0
** The stoneworth spawning spots
	pat: dull 3 "gp_cranstone_planter_JESS" 1 19 39 1
	pat: dull 4 "gp_cranstone_planter_JESS" 1 31 32 1
	pat: dull 5 "gp_cranstone_planter_JESS" 1 40 44 1
** The cranberry spawning spots
	pat: dull 6 "gp_cranstone_planter_JESS" 6 3 29 1
	pat: dull 7 "gp_cranstone_planter_JESS" 6 5 14 1
	pat: dull 8 "gp_cranstone_planter_JESS" 6 12 17 1

	tick 20
	mvsf _p1_ _p2_
	slow
	targ ownr
	kill targ
endm


**
** PLANTER PUSH SCRIPT
**

scrp 2 23 21830 1
	mesg writ pntr 101
	setv va05 posx
	setv va06 posy
** Vend a cranberry
	new: simp 2 8 21830 "gp_cranstone_planter_JESS" 1 10 rand 1800 1900
** attr: mouseable 2 + collide 64 + physics 128 = 194
	attr 194
** Will go through some walls
	perm 80
** Physics
	accg 0.7
	aero 1
	fric 60
	elas 40
** bhvr: pickup 16 + eat 32 = 48
	bhvr 48
	tick 1800
	velo rand -1 1 -1
	mvsf va05 va06
endm

**
** PLANTER TIMER SCRIPT
**

scrp 2 23 21830 9
	doif carr eq null
		tick rand 160 260
		lock

** Grow the flowers and spawn the seed
		setv va01 3
		reps 6
			part va01
** stoneworth subroutine
			doif va01 lt 6
** pose ge 1 --> grow to next stage
				doif pose ge 1 and pose lt 3
					setv va10 pose
					addv va10 1
					pose va10
** pose eq 3 --> reset lobe and drop seed
				elif pose eq 3
** Create and drop a new seed
					gsub dropedible
** reset seed lobe
					targ ownr
					part va01
					pose 0
				endi
** cranberry subroutine
			else
				doif pose ge 1 and pose lt 3
					setv va10 pose
					addv va10 1
					pose va10
				elif pose eq 3
					gsub dropedible
** reset seed lobe
					targ ownr
					part va01
					pose 0
				endi
			endi
			addv va01 1
			targ ownr

		repe


** Check how many seed lobes there are currently growing
		setv va00 0
		setv va01 3
		reps 3
			part va01
			doif pose ne 0
				addv va00 1
			endi
			addv va01 1
		repe

** If there are less than 3 seed lobes growing, there may grow another one.
		doif va00 lt 3 and rand 0 2 eq 0
** Make the first checking point random.
			setv va01 3
			addv va01 rand 0 2
			setv va12 0
** If the first spot isn't free, try the next one.
			reps 3
				part va01
				doif pose eq 0 and va12 eq 0
					pose 1
					addv va12 1
				endi
				addv va01 1
				doif va01 gt 5
					setv va01 3
				endi
			repe
		endi

** Check how many cranberrys there are currently growing
		setv va02 0
		setv va03 6
		reps 3
			part va03
			doif pose ne 0
				addv va02 1
			endi
			addv va03 1
		repe
** If there are less than 3 cranberrys growing, there may grow another one.
		doif va00 lt 3 and rand 0 2 eq 0
			setv va13 0
** Make the first checking point random.
			setv va01 6
			addv va01 rand 0 2
** If the first spot isn't free, try the next one.
			reps 3
				part va01
				doif pose eq 0 and va13 eq 0
					pose 1
					addv va13 1
				endi
				addv va01 1
				doif va01 gt 8
					setv va01 6
				endi
			repe
		endi
	endi

	subr dropedible
		inst
		setv va05 posl
		setv va06 post
		doif va01 lt 6
** stoneworth 3 4 5
			doif va01 eq 3
				addv va05 19
				addv va06 39
			elif va01 eq 4
				addv va05 31
				addv va06 32
			elif va01 eq 5
				addv va05 40
				addv va06 44
			endi
			new: simp 2 3 21830 "gp_cranstone_planter_JESS" 1 5 rand 1200 1900

		else
** cranberrys 6 7 8
			doif va01 eq 6
				addv va05 3
				addv va06 29
			elif va01 eq 7
				addv va05 5
				addv va06 14
			elif va01 eq 8
				addv va05 12
				addv va06 17
			endi
			new: simp 2 8 21830 "gp_cranstone_planter_JESS" 1 10 rand 1200 1900

		endi

** attr: mouseable 2 + collide 64 + physics 128 = 194
** bhvr: eat 16 + pickup 32 = 48
		attr 194
		bhvr 48
** Will go through some walls
		perm 80
** Physics
		accg 0.7
		aero 1
		fric 60
		elas 40
		tick rand 800 1400
		velo rand -1 1 0
		mvsf va05 va06
		slow
	retn
endm


**
** stoneworth SEED TIMER
**

**
** cranberry TIMER
**

**
** stoneworth SEED EAT
**
scrp 2 3 21830 12
	lock
	wait 10
** stim eat plant
	stim writ from 77 1
	wait 10
	kill ownr
	unlk
endm

**
** cranberry EAT
**

scrp 2 8 21830 12
	lock
	wait 10
** stim eat fruit
	stim writ from 78 1
	wait 10
	kill ownr
	unlk
endm

**
** stoneworth SEED TIMER
**

scrp 2 3 21830 9
	doif carr eq null
		kill ownr
	endi
endm


**
** cranberry TIMER
**

scrp 2 8 21830 9
	doif carr eq null
		kill ownr
	endi
endm


*
** Removal script 
*

rscr

enum 2 23 21830
	kill targ
next
enum 2 3 21830
	kill targ
next
enum 2 8 21830
	kill targ
next

scrx 2 23 21830 1
scrx 2 23 21830 9

scrx 2 3 21830 9
scrx 2 3 21830 12
scrx 2 8 21830 9
scrx 2 8 21830 12