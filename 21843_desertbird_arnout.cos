*Drumstick Bird Egg: 2 18 21843
*Drumstick Bird Young: 2 14 21843, with cato 15 to classify it as critter
*Drumstich Bird Mature: 2 15 21843

*Drumstick Population Checker: 1 1 21843
*Every 5 minute, this agent checks the Drumstick Bird population

*new: simp 1 1 21843 "blnk" 1 0 0
*mvto va50 va51
*tick 20

*new: simp 1 1 21843 "blnk" 1 1 0
*setv ov20 22000
*setv ov21 3500
*mvto ov20 ov21
*tick 20


scrp 1 1 21843 9
	inst
	tick 1500

	setv va00 gmap posx posy
	doif carr eq null
		doif va00 eq -1
			kill ownr
			stop
		endi
	endi

	setv va20 ov20
	setv va21 ov21
*Should the Drumstick Bird population die out, then this agent will make new eggs
	setv va97 0
	enum 2 14 21843
		doif gmap posx posy eq va00
			addv va97 1
		endi
	next
	enum 2 15 21843
		doif gmap posx posy eq va00
			addv va97 1
		endi
	next
	enum 2 18 26503
		doif gmap posx posy eq va00
			addv va97 1
		endi
	next

	doif va97 lt 4
*make 2
		reps 2
			new: simp 2 18 21843 "gp_desertstickbird_CS" 1 0 999
*attr 1 + 2 + 64 + 128
			attr 195
			bhvr 48
			perm 100
			accg 2
			aero 10
			fric 100
			elas 0
*set a timer between 5 and 10 secs
			tick rand 200 600
*move to the intended spots
			velo rand -10 10 rand -3 0
			mvsf va20 va21
			slow
		repe
	endi
*If the total population exceeds 19, make all adults in that spot infertile.
	doif va97 gt 19
		tick 1200
		enum 2 15 21843
			doif gmap posx posy eq ov00
				setv ov02 0
			endi
		next
	endi
endm

*egg hatch (timer) script
scrp 2 18 21843 9
	inst
*set current position to 2 variables
	setv va00 posx
	setv va01 post
	subv va01 5
*create a chick
	new: simp 2 14 21843 "gp_desertstickbird_CS" 24 1 1001
	cato 15
*attr 1 + 2 + 4 + 64 + 128
	attr 199
	bhvr 59
	perm 100
	accg 1
	aero 10
	fric 100
	elas 0
*timer: 1 second
	tick 20
	setv ov10 rand 0 1
	doif ov10 eq 0
		setv ov10 -1
	endi
	doif tmvt va00 va01 eq 1
*move it to the position equal to the 2 variables
		mvsf va00 va01
	else
		kill targ
		kill ownr
		stop
	endi
*get rid of the egg shell
	kill ownr
	slow
endm

*egg eat script
scrp 2 18 21843 12
*prevent other scripts from interfering
	lock
	inst
*play a crunchy sound
	snde "reat"
*Stim the Creature with "eaten critter"
	stim writ from 80 1
*get rid of the egg shell
	kill ownr
	slow
endm

*Push script bird
scrp 2 14 21843 1
	inst
*check if it's a Creature
	targ from
	doif fmly eq 4
*Stim it with "played with critter"
		stim writ targ 86 1
	endi
*retarget the owner
	targ ownr
	velo 0 rand -3 -7
	slow
endm

*Pull script bird
scrp 2 14 21843 2
	inst
	targ from
	doif fmly eq 4
		stim writ targ 86 1
	endi
	targ ownr
	velo 0 rand -3 -7
	slow
endm

*Hit script bird
scrp 2 14 21843 3
	inst
	targ from
	doif fmly eq 4
*Stim it with "hit critter"
		stim writ targ 87 1
	endi
	targ ownr
	velo 0 rand -3 -7
	slow
endm

*Eat script bird
scrp 2 14 21843 12
*Stop other scripts from interfering
	lock
	inst
*Play another crunchy sounds
	snde "chwp"
*Stim the Creature with "eaten critter"
	stim writ from 80 1
*get rid of the bird
	kill ownr
	slow
endm

*Timer and behaviour script
scrp 2 14 21843 9
	inst
*check if being carried or falling
	doif carr <> null and fall <> 0
		stop
	endi
	gsub rcheck
	gsub age
	gsub move

	subr rcheck
*drown in water
		doif rtyp room targ eq 8 or rtyp room targ eq 9
			kill ownr
		endi
	retn

	subr age
		addv ov00 rand 1 3
		doif ov00 > 200
			inst
			setv va02 posx
			setv va03 post
			subv va03 40
*create an adult bird
			new: simp 2 15 21843 "gp_desertstickbird_CS" 43 25 1000
*attr 1 + 2 + 4 + 64 + 128
			attr 199
			bhvr 59
			perm 100
			accg 1
			aero 10
			fric 100
			elas 0
*timer: 1 second
			tick 20
			setv ov10 rand 0 1
			doif ov10 eq 0
				setv ov10 -1
			endi
			doif tmvt va02 va03 eq 1
				mvsf va02 va03
			else
				kill ownr
				kill targ
			endi
*get rid of the egg shell
			kill ownr
			slow
		endi
	retn

	subr move
		doif ov10 eq -1
			reps rand 1 4
				doif carr = null and fall = 0
					anim [12 13 14 15 16 17 18 19 20 21 22 23 12]
					velo -3 -2
					over
				endi
				addv ov73 rand 1 3
			repe
			setv ov72 0
		elif ov10 eq 1
			reps rand 1 4
				doif carr = null and fall = 0
					anim [0 1 2 3 4 5 6 7 8 9 10 11]
					velo 3 -2
					over
				endi
				addv ov72 rand 1 3
			repe
			setv ov73 0
		endi
		doif ov72 > 12
			setv ov10 -1
		elif ov73 > 12
			setv ov10 1
		endi
	retn
endm

*Push script adult bird
scrp 2 15 21843 1
	inst
*check if it's a Creature
	targ from
	doif fmly eq 4
*Stim it with "played with critter"
		stim writ targ 86 1
	endi
*retarget the owner
	targ ownr
	velo 0 rand -3 -7
	slow
endm

*Pull script adult bird
scrp 2 15 21843 2
	inst
	targ from
	doif fmly eq 4
		stim writ targ 86 1
	endi
	targ ownr
	velo 0 rand -3 -7
	slow
endm

*Hit script adult bird
scrp 2 15 21843 3
	inst
	targ from
	doif fmly eq 4
*Stim it with "hit critter"
		stim writ targ 87 1
	endi
*Peck!
	velo 0 rand -2 -5
	snde "stng"
	chem 148 0.2
	slow
endm

*Eat script adult bird
scrp 2 15 21843 12
*Stop other scripts from interfering
	lock
	inst
*Play another crunchy sounds
	snde "chwp"
*Stim the Creature with "eaten critter"
	stim writ from 80 1
*get rid of the bird
	kill ownr
	slow
endm

*Timer script adult bird
scrp 2 15 21843 9
	inst
*check if being carried or falling
	doif carr <> null and fall <> 0
		stop
	endi
	gsub roomc
	gsub die
	doif ov02 gt 220
		gsub egg
	endi
	gsub move

	subr roomc
*drown in water
		doif rtyp room targ eq 8 or rtyp room targ eq 9
			kill ownr
		endi
	retn

	subr die
		addv ov01 rand 1 3
		addv ov02 rand 1 4
		doif ov01 gt 400
			kill ownr
		endi
	retn

	subr egg
		lock
		doif ov10 eq 1
			anim [18 19 20 21 22 23]
		elif ov10 eq -1
			anim [31 32 33 34 35 36]
		endi
		over
		setv va33 posx
		setv va34 posy
		inst
		setv va55 0
		enum 2 15 21843
			doif gmap posx posy eq gmap va33 va34
				addv va55 1
			endi
		next
		enum 2 14 21843
			doif gmap posx posy eq gmap va33 va34
				addv va55 1
			endi
		next

		doif va55 lt 12

			new: simp 2 18 21843 "gp_desertstickbird_CS" 1 0 999
			attr 195
			bhvr 48
			perm 100
			accg 2
			aero 10
			fric 100
			elas 0
			doif tmvt va33 va34 eq 1
				mvsf va33 va34
				tick rand 200 600
			else
				kill targ
			endi
		endi
		slow
		targ ownr
		doif ov10 eq 1
			anim [24 25 26 27 28 29 30 0]
		elif ov10 eq -1
			anim [37 38 39 40 41 42 9]
		endi
		over
		setv ov02 0
		unlk
	retn

	subr move
		doif ov10 eq -1
			reps rand 1 4
				doif carr = null and fall = 0
					anim [9 10 11 12 13 14 15 16 17]
					velo -6 rand -5 -6
					over
				endi
				addv ov73 rand 1 3
			repe
			setv ov72 0
		elif ov10 eq 1
			reps rand 1 4
				doif carr = null and fall = 0
					anim [0 1 2 3 4 5 6 7 8]
					velo 6 rand -5 -6
					over
				endi
				addv ov72 rand 1 3
			repe
			setv ov73 0
		endi
		doif ov72 > 15
			setv ov10 -1
		elif ov73 > 15
			setv ov10 1
		endi
	retn
endm

*remove script
rscr
enum 2 18 21843
	kill targ
next
scrx 2 18 21843 9
scrx 2 18 21843 12
enum 2 14 21843
	kill targ
next
scrx 2 14 21843 1
scrx 2 14 21843 2
scrx 2 14 21843 3
scrx 2 14 21843 9
scrx 2 14 21843 12
enum 2 15 21843
	kill targ
next
scrx 2 15 21843 1
scrx 2 15 21843 2
scrx 2 15 21843 3
scrx 2 15 21843 9
scrx 2 15 21843 12
enum 1 1 21843
	kill targ
next
scrx 1 1 21843 9