**
**
**
**
** GP grendel hideout berries
**
**
**
**

**
**
**
**
** GP grendel hideout berries
**
** Create berry: 
*	ov04 base
*	ov05 posx
*	ov06 width
*	ov07 posy
*	ov08 height
**
**

** Scripts
**

*
** Berry creator timer script
*
scrp 1 1 21847 9
	rnge 1600
	setv va00 0
	inst
	esee 2 8 21847
		doif attr ne 80
			addv va00 1
		endi
	next
	slow
	doif va00 lt 5
* Set pose variables
		setv va00 ov04
		mulv va00 3
* Set dimension variables posx width posy length
		setv va01 ov05
		addv va01 rand 0 ov06
		setv va02 ov07
		addv va02 rand 0 ov08
* Create patch berry
		new: simp 2 8 21847 "gp_c2berries" 5 9 rand 50 1000
		attr 80
		perm rand 20 100
		emit 6 .3
		elas 25
		fric 90
		setv ov01 0
		tick 1
		base va00
		pose 0
		mvsf va01 va02

	endi
	targ ownr
	tick rand 180 400
endm


*
** Berry timer script
*

scrp 2 8 21847 9
	doif pose lt 4
		setv ov03 0
		tick rand 200 400
		addv ov01 1
		pose ov01
	else
		setv ov00 1
		addv ov03 1
		attr 199
		accg 3
		aero 2
		bhvr 48
		doif ov03 gt 5
			setv va00 posl
			setv va01 post
			setv va02 perm
			new: simp 2 10 21847 "gp_c2berries_rotten" 1 1 rand 500 1000
			mvsf va00 va01
			perm va02
			tick rand 400 600
			kill ownr
		endi
	endi
endm


*
** Rotten tick
*

scrp 2 10 21847 9
kill ownr
endm

*
** Rotten creation
*

scrp 2 10 21847 10
inst
		attr 199
			elas 25
		fric 90
	accg 3
		aero 2
		bhvr 48
	emit 4 0.6
slow
endm

*
** Rotten eat script
*

scrp 2 10 21847 12
	sndc "chwp"
	stim writ from 81 .5
	chem 5 1
	chem 10 .2
	chem 12 .2
	chem 5 .2
	targ ownr
	kill targ
endm


*
** Berry eat script
*

scrp 2 8 21847 12
	sndc "chwp"
	stim writ from 78 .5
	chem 5 1
	chem 10 .2
	chem 12 .2
	chem 5 .2
	targ ownr
	kill targ
endm

**
** Removal script
**
rscr

* Remove berry creator
enum 1 1 21847
	kill targ
next

* Remove berries
enum 2 8 21847
	kill targ
next